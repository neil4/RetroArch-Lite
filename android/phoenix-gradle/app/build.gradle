apply plugin: 'com.android.application'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.retroarchlite"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29

        ndk {
            moduleName "retroarch-jni"
        }

        externalNativeBuild {
            ndkBuild {
                arguments "-j${Runtime.runtime.availableProcessors()}"
            }
        }
    }

    flavorDimensions "abi"
    productFlavors {
        ral32 {
            dimension "abi"
            ndk {
                abiFilters 'armeabi-v7a'
            }
            resValue "string", "app_name", "RetroArch Lite (32-bit)"
            resValue "string", "app_id", "com.retroarchlite"
            resValue "string", "shared_app_id", "com.retroarchlite64"
        }
        ral64 {
            dimension "abi"
            ndk {
                abiFilters 'arm64-v8a'
            }
            applicationId "com.retroarchlite64"
            resValue "string", "app_name", "RetroArch Lite"
            resValue "string", "app_id", "com.retroarchlite64"
            resValue "string", "shared_app_id", "com.retroarchlite"
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path file('src/main/jni/Android.mk')
        }
    }

    sourceSets {
        ral32 {
            res.srcDirs = ['libs/googleplay/res']
        }
        ral64 {
            res.srcDirs = ['libs/googleplay/res']
        }
    }


    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.0'
}
